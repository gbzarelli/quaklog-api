//https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/#getting-started
plugins {
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

sourceCompatibility = '17'

//Generate jar with other name
bootJar {
    archiveFileName = "${rootProject.ext.archive_file_name}"
}

//task to copy jar to root build dir
tasks.register('copyBootJar', Copy) {
    from bootJar
    into rootProject.buildDir
}

//Execute copy after compile
compileJava.finalizedBy copyBootJar

//Automatic Property Expansion Using Gradle
// (https://docs.spring.io/spring-boot/docs/current/reference/html/howto-properties-and-configuration.html)
processResources {
    filesMatching("application.yml") {
        expand('project': rootProject.properties)
    }
}

dependencies {
    implementation project(":core")

    implementation libs.database
    implementation libs.spring_config
    implementation libs.cacheable
    implementation libs.entrypoint
    implementation libs.communs_libs
    implementation libs.inject

    annotationProcessor libs.spring_annotation_processor
    annotationProcessor libs.communs_annotation_processor

    testImplementation libs.spring_test
    testImplementation libs.unit_test
}
